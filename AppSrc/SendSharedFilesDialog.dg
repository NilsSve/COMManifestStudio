Use cRDCDbModalPanel.pkg
Use cRDCButton.pkg
Use cManifestIniFile.pkg
Use cLinkLabel.pkg

CompilerWarnings Suspend
Use cMapiHandler.Pkg
CompilerWarnings Restart

Use vWin32fh.pkg
Use Symdef.pkg

Object oSendSharedFiles_Dialog is a cRDCDbModalPanel
    Set Label to "Share New Manifest Fragment Files"
    Set Size to 151 309
    Set Icon to "FragmentShare.ico"
    Set Border_Style to Border_Dialog

    Object oQuestion_tb is a TextBox
        Set Size to 19 281
        Set Location to 10 15
        Set Auto_Size_State to False
        Set Justification_Mode to JMode_Left
        Set Label to "Press the 'Send Now!' button to share new 'Manifest Fragment Files' you have created with other users of this program."
    End_Object

    Object oInfo_tb is a TextBox
        Set Size to 40 281
        Set Location to 32 15
        Set Auto_Size_State to False
        Set Justification_Mode to JMode_Left
        Set Label to "When you press the 'Send Now!' button you will be presented with an e-mail dialog where you can drag && drop new Manifest Fragment Files from your local copy of the 'Manifest Fragment Library' to the e-mail client and send them to the author of this program who will check them and add them to the VDF-Guidance 'Manifest Fragment Library' global repository."
    End_Object

    Object oQuestion2_tb is a TextBox
        Set Size to 24 281
        Set Location to 77 15
        Set Auto_Size_State to False
        Set Justification_Mode to JMode_Left
        Set Label to "Note: If no e-mail dialog appears it is probably because you have a 64-bit e-mail client. This program is 32-bit and a Windows limitation makes it impossible for a 32-bit program to communicate to a 64-bit client with simple mapi calls.  If so try this link:"
    End_Object

    Object oLink_Label is a cLinkLabel
        Set Size to 8 281
        Set Location to 102 15
        Set Label to ('<A HREF="mailto:' + CS_NewFragmentFileEmail + '?Subject=' + CS_AutoGeneratedHeader + '&body=' + CS_AutoGeneratedBody + '">Alternate solution for sending New Manifest Fragment File(s)</A>')
        Procedure OnClick Integer iItem String sID String sUrl
            String sName sPath
            // First show Windows Explorer with the local 'Manifest Fragment Library', so user can drag & drop files from there
            // to the e-mail client.
            Get psManifestFragmentLibrary of ghoManifestFunctionLibrary to sPath
            Send vShellExecute "open" "Explorer.exe" sPath ""
            Get Network_User_Name to sName
            Move (sUrl * "Shared by:" * sName) to sUrl
            Runprogram Shell Background sUrl
            Send Close_Panel
        End_Procedure
    End_Object

    Object oOk_btn is a cRDCButton
        Set Location to 127 191
        Set Label to "Send Now!"
        Set peAnchors to anBottomRight
        Procedure OnClick
            Send DoSendManifestFragmentFiles
        End_Procedure
    End_Object

    Object oClose_btn is a cRDCButton
        Set Size to 14 50
        Set Label    to "Close"
        Set Status_Help to "Close Panel"
        Set Location to 127 246
        Set peAnchors to anBottomRight
        Set Default_State to True
        Procedure OnClick
           Send Close_Panel
        End_Procedure
    End_Object

    Procedure DoSendManifestFragmentFiles
        Integer iRetval
        Handle hoMapiSession
        String sName sPath

        // First show Windows Explorer with the local 'Manifest Fragment Library', so user can drag & drop files from there
        // to the e-mail client.
        Get psManifestFragmentLibrary of ghoManifestFunctionLibrary to sPath
        Send vShellExecute "open" "Explorer.exe" sPath ""

        Get Network_User_Name to sName
        Get Create U_cMapiHandler to hoMapiSession
        Send DoInit of hoMapiSession
        Set psMessageSubject of hoMapiSession to CS_AutoGeneratedHeader
        Set psMessageText    of hoMapiSession to (CS_AutoGeneratedBody + CR_LF + CR_LF + "Note to the sender: Thank you for sharing! Drag and drop the Manifest Fragment Files you have created onto this message, to send them as attachments." + CR_LF + CR_LF + "Shared by," + CR_LF + sName)
        Send DoAddReceiver   of hoMapiSession MAPI_TO "" ("SMTP:" + CS_NewFragmentFileEmail) "" 0
        Get Logon            of hoMapiSession "" "" 0 to iRetval
        Get SendMail         of hoMapiSession MAPI_DIALOG True to iRetval
        Get Logoff           of hoMapiSession to iRetval
        Send Destroy         of hoMapiSession

        Send Close_Panel
    End_Procedure

    On_Key Key_Alt+Key_Y  Send KeyAction of oOk_btn
    On_Key Key_Ctrl+Key_Y Send KeyAction of oOk_btn
    On_Key Key_Alt+Key_C  Send KeyAction of oClose_btn
    On_Key Key_Ctrl+Key_C Send KeyAction of oClose_btn
    On_Key Key_Escape     Send KeyAction of oClose_btn
End_Object
