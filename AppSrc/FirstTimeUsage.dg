Use seq_chnl.pkg    // Sequential channel functions
Use cRDCDbModalPanel.pkg
Use cRDCDbRichEditor.pkg
Use cRDCButton.pkg
Use cRDCCheckbox.pkg
Use cManifestIniFile.pkg
Use ImageViewer.dg
Use Symdef.pkg
Use vWin32fh.pkg

Object oFirstTimeUsage_Dialog is a cRDCDbModalPanel
    Set Label to "First Time Usage Information"
    Set Size to 218 367
    Set Icon to "ActionHelp.ico"

    Object oInfo_Editor is a cRDCDbRichEditor
        Set Size to 185 358
        Set Location to 4 5
        Set peAnchors to anAll
        Set Read_Only_State to True
        Set peAnchors to anAll
        Set Border_Style to Border_StaticEdge
        Set TextBackColor to clHighlight
        Set TextColor to clHighlightText
        Set ReadOnlyColor to clWhite

        // Dummy message. It is here for the compiler to embed the help file
        // into the program.
        // The compiler will pick up the include_resource and read the file from
        // the _Help_ folder.
        Procedure _DummyEmbedFile
            Include_Resource ..\Help\StartUpInfo.rtf as res_StartUpInfo Type DF_RESOURCE_TYPE_BINARY
        End_Procedure

        Procedure DoReadFile String sFileName Boolean bEmbedFile
            If (bEmbedFile = True) Begin
                Move  "Resource:res_StartUpInfo" to sFileName
            End
            Forward Send DoReadFile sFileName bEmbedFile
        End_Procedure

        On_Key kCancel Send Close_Panel
        On_Key kEnter  send Close_Panel
    End_Object

    Object oFirstTimeUsage_cb is a CheckBox
        Set Size to 12 50
        Set Location to 197 5
        Set Label to "Show this dialog on startup"
        Set peAnchors to anBottomLeft

        Procedure OnChange
            Boolean bChecked
            String  sState
            Get Checked_State to bChecked
            If (bChecked = True) Begin
                Move CS_BooleanYes to sState
            End
            Else Begin
                Move CS_BooleanNo to sState
            End
            Set IniFileValue of ghoManifestIniFile (psSectionName(ghoManifestIniFile)) CS_FirstTimeUsageText to sState
        End_Procedure

        Procedure OnStartup
            Boolean bChecked
            String  sYes
            Get IniFileValue of ghoManifestIniFile (psSectionName(ghoManifestIniFile)) CS_FirstTimeUsageText CS_BooleanYes to sYes
            Move (CS_BooleanYes = sYes) to bChecked
            Set Checked_State to bChecked
        End_Procedure
        Send OnStartup

        On_Key kCancel Send Close_Panel
    End_Object

    Object oImage_btn is a cRDCButton
        Set Location to 196 257
        Set Label to "View Image"
        Set Status_Help to "View Side-By-Side Assemblies and Manifests schema (F1)"
        Set peAnchors to anBottomRight
        Procedure OnClick
            Send Popup of (oImageViewer(Self))
        End_Procedure

        On_Key kCancel Send Close_Panel
    End_Object

    Object oClose_btn is a cRDCButton
        Set Size to 14 50
        Set Label    to "Close"
        Set Status_Help to "Close Panel"
        Set Location to 196 313
        Set peAnchors to anBottomRight
        Set Default_State to True

        Procedure OnClick
           Send Close_Panel
        End_Procedure

        On_Key kCancel Send Close_Panel
    End_Object

    Procedure Activating
        Set TextFile of oInfo_Editor to CS_StartUpInfo True
        Forward Send Activating
    End_Procedure

//    On_Key Key_Alt+Key_O Send KeyAction of oOk_btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_btn
    On_Key Key_F1        Send KeyAction of oImage_btn
End_Object
